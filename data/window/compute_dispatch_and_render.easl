@{address storage-write
  group 0
  binding 0}
(var my-array: [16: vec4f])

@{address uniform
  group 0
  binding 1}
(var frame-index: u32)

@compute
@{workgroup-size 16}
(defn c [@{builtin global-invocation-id}
         id: vec3u]: ()
  (let [@var x 0.25]
    (= (my-array id.x)
       (vec4f (/ (vec3f (% (+ id.x (/ frame-index 15))
                           16u))
                 16.)
              1.))))

@vertex
(defn v [@{builtin vertex-index}
         vertex-index: u32]: @{builtin position} vec4f
  (vec4f (match vertex-index
           0 (vec2f -1.)
           1 (vec2f -1. 3.)
           _ (vec2f 3. -1.))
         0.
         1.))

@fragment
(defn f [@{builtin position} pos: vec4f]: @{location 0} vec4f
  (my-array (u32 (* pos.x 0.01))))

@cpu
(defn main []
  (spawn-window (fn []
                    (+= frame-index 1u)
                    (dispatch-compute-shader c (vec3u 1u 1u 1u))
                    (dispatch-render-shaders v f 3))))
